dnl Process this file with autoconf to produce a configure script.
# $Id: configure.in,v 1.1.1.1 2003/05/07 02:15:06 lfan Exp $
#
# Copyright 2001-2003 Double Precision, Inc.  See COPYING for
# distribution information.

AC_INIT(pcp.h)
AM_INIT_AUTOMAKE(pcp, 0.10)
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_ISC_POSIX
AC_PROG_CC
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PATH_PROGS(PERL, perl5 perl, perl)

CFLAGS="-I.. -I${srcdir}/.. $CFLAGS"
ALL_LINGUAS=""
AM_GNU_GETTEXT

dnl Checks for libraries.

USENSL=no
saveLIBS="$LIBS"
AC_CHECK_LIB(socket,socket,result=yes,result=no)
if test $result = yes; then
        NETLIBS="-lsocket"
else
        AC_CHECK_LIB(socket,socket,result=yes,result=no,-lnsl)
        if test $result = yes; then
                NETLIBS = "-lsocket -lnsl"
                USENSL=yes
        else
                AC_CHECK_LIB(socket,connect,result=yes,result=no)
                if test $result = yes; then
                        NETLIBS="-lsocket"
                else
                        AC_CHECK_LIB(socket,connect,result=yes,result=no,-lnsl)
                        if test $result = yes; then
                                NETLIBS="-lsocket -lnsl"
                                USENSL=yes
                        fi
                fi
        fi
fi

if test $USENSL != yes; then
	LIBS="$LIBS $NETLIBS"
	AC_TRY_LINK_FUNC(inet_addr, [ : ],
	[
	        AC_CHECK_LIB(nsl,inet_addr,result=yes,result=no)
	        if test $result = yes; then
	                NETLIBS="$NETLIBS -lnsl"
	        fi
	])
fi

LIBS="$saveLIBS"

echo $NETLIBS >pcp.libs
AC_SUBST(NETLIBS)

if test -d ${srcdir}/../courier
then
        scriptdir="$datadir/sqwebmail"
else
        scriptdir="$prefix"
fi

SENDIT="$scriptdir/sendit.sh"
AC_SUBST(SENDIT)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h syslog.h sys/stat.h sys/time.h termios.h unistd.h)
AC_HEADER_DIRENT
AC_HEADER_TIME

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_UID_T
AC_TYPE_PID_T
AC_TYPE_SIGNAL
AC_STRUCT_TM

dnl Checks for library functions.
AC_CHECK_FUNCS(gethostname strdup tcgetattr)

AC_ARG_WITH(calendardir,
[  --with-calendardir=path    - use path instead of prefix/var/spool/calendar],
	calendardir="$withval",
	calendardir="$localstatedir/calendar")
AC_SUBST(calendardir)

localpcpdcachedir="$calendardir/localcache"
AC_SUBST(localpcpdcachedir)

AC_ARG_WITH(calendarpurge,
[  --with-calendarpurge=N     - purge past events over N days old (default: 30)],
	calendarpurge="$withval",
	calendarpurge=30)

AC_DEFINE_UNQUOTED(CALENDARPURGE,$calendarpurge,
	[ Purge calendar entries after this many days ])

# TODO

AC_ARG_ENABLE(hardtimeout,
	[  --enable-hardtimeout=n     Timeout after n seconds (min 1800)],
	TIMEOUT=$enableval, TIMEOUT=1800)
if test $TIMEOUT -lt 1800
then
	TIMEOUT=1800
fi
AC_SUBST(TIMEOUT)

AC_ARG_WITH(cacheowner,
	[  --with-cacheowner=user     Owner of the cache directory (default: bin)],
        localcacheowner="$withval", localcacheowner="bin")

AC_SUBST(localcacheowner)
AC_DEFINE_UNQUOTED(LOCALCACHEOWNER,"$localcacheowner",
	[ Owner of the login cache directory ])

AC_ARG_WITH(mailgroup,
	[],
	mailgroup="$withval",

	for mailgroup in mail sys bin
	do
		if $PERL -e ' getgrnam("'$mailgroup'") && exit 0; exit 1;'
		then
			break;
		fi
	done
)
AC_SUBST(mailgroup)
AC_DEFINE_UNQUOTED(MAILGROUP,"$mailgroup",
	[ Mail system group ])

AC_TRY_COMPILE( [
#include <signal.h>
#include <string.h>
#include <stdio.h>
],[
struct sigaction sa;

memset(&sa, 0, sizeof(sa));
sa.sa_handler=SIG_IGN;
sa.sa_flags=SA_NOCLDWAIT;
sigaction(SIGCHLD, &sa, NULL);
], AC_DEFINE_UNQUOTED(USE_NOCLDWAIT,1, [ Whether to use SA_NOCLDWAIT ]))

if test -f $srcdir/.debug
then
	EFENCE="-lefence"
fi
AC_SUBST(EFENCE)

if test x$GCC = xyes
then
	CFLAGS="-Wall $CFLAGS"
fi

CPPFLAGS="-Iintl/ -I${srcdir}/intl $CPPFLAGS"

AC_OUTPUT(Makefile po/Makefile.in m4/Makefile   intl/Makefile )
